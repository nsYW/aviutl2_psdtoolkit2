# bundle_lua.cmake
#
# Bundles multiple Lua files into a single Lua file using package.preload.
#
# Inputs:
#   -Dinput_dir=<dir>           Directory containing .lua files to bundle.
#   -Doutput_file=<path>        Output .lua path.
#   -Dmodule_name=<name>        Base module name.
#   -Dversion_env_file=<path>   (optional) Path to version.env file to read version info from.

cmake_minimum_required(VERSION 3.10)

if(NOT DEFINED input_dir OR input_dir STREQUAL "")
  message(FATAL_ERROR "input_dir is required")
endif()
if(NOT DEFINED output_file OR output_file STREQUAL "")
  message(FATAL_ERROR "output_file is required")
endif()
if(NOT DEFINED module_name OR module_name STREQUAL "")
  message(FATAL_ERROR "module_name is required")
endif()

# Read version from version.env file if provided.
set(version "")
if(DEFINED version_env_file AND EXISTS "${version_env_file}")
  file(STRINGS "${version_env_file}" version_env_lines)
  foreach(line IN LISTS version_env_lines)
    if(line MATCHES "^PTK_VERSION=(.+)$")
      set(version "${CMAKE_MATCH_1}")
      break()
    endif()
  endforeach()
endif()

file(GLOB_RECURSE lua_files LIST_DIRECTORIES false "${input_dir}/*.lua")
list(SORT lua_files)

# Generate header comment with output filename and version.
get_filename_component(output_filename "${output_file}" NAME)
set(header_version "${version}")
if(header_version STREQUAL "")
  set(header_version "%VERSION%")
endif()
set(bundle_content "--- ${output_filename} ${header_version} by oov\n--- DO NOT EDIT - This file is automatically generated.\n")
set(init_content "")

foreach(lua_file IN LISTS lua_files)
  file(RELATIVE_PATH rel_path "${input_dir}" "${lua_file}")
  
  get_filename_component(file_dir "${rel_path}" DIRECTORY)
  get_filename_component(file_name "${rel_path}" NAME_WE)
  
  if(file_name STREQUAL "init" AND file_dir STREQUAL "")
    set(current_module_name "${module_name}")
    set(is_init TRUE)
  else()
    string(REPLACE "/" "." sub_module "${file_dir}")
    if(sub_module STREQUAL "")
      set(current_module_name "${module_name}.${file_name}")
    else()
      set(current_module_name "${module_name}.${sub_module}.${file_name}")
    endif()
    set(is_init FALSE)
  endif()
  
  file(READ "${lua_file}" content)
  
  if(is_init)
    set(init_content "${content}")
  else()
    string(APPEND bundle_content "package.preload['${current_module_name}'] = function(...)\n${content}\nend\n\n")
  endif()
endforeach()

if(init_content STREQUAL "")
  message(FATAL_ERROR "init.lua not found in ${input_dir}")
endif()

string(APPEND bundle_content "${init_content}")

# Replace %VERSION% with actual version string if provided.
if(NOT version STREQUAL "")
  string(REPLACE "%VERSION%" "${version}" bundle_content "${bundle_content}")
endif()

file(WRITE "${output_file}" "${bundle_content}")
